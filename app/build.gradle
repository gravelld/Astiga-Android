apply plugin: 'com.android.application'

repositories {
	maven {
		url "https://maven.google.com"
	}
	maven { url 'https://dl.bintray.com/guardian/android' }
	maven { url 'https://jitpack.io' }
}

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
	compileSdkVersion 34

	defaultConfig {
		applicationId "ga.asti.android"
		minSdkVersion 16
		targetSdkVersion 34
		// this should just be incremented on each new build. it need have no relation to the versionName https://developer.android.com/studio/publish/versioning
		versionCode 10539
		versionName '1.5.38'
		setProperty("archivesBaseName", "Astiga $versionName")
		//resConfigs "de", "es", "fr", "hu", "nl", "pt-rPT", "ru", "sv"
		vectorDrawables.useSupportLibrary = true
		resValue 'string', 'account_type.subsonic', applicationId + ".subsonic"
		resValue 'string', 'provider.search', applicationId + ".provider.DSubSearchProvider"
		resValue 'string', 'provider.playlist', applicationId + ".playlists.provider"
		resValue 'string', 'provider.podcast', applicationId + ".podcasts.provider"
		resValue 'string', 'provider.starred', applicationId + ".starred.provider"
		resValue 'string', 'provider.recently_added', applicationId + ".mostrecent.provider"
	}
    signingConfigs {
        release {
            // You need to specify either an absolute path or include the
            // keystore file in the same directory as the build.gradle file.
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
	buildTypes {
		release {
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
			zipAlignEnabled true
			signingConfig signingConfigs.release
			debuggable = false
		}
		fix {
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
			zipAlignEnabled true
		}
		debug {
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
			zipAlignEnabled true
		}
	}
	flavorDimensions "version"
	productFlavors {
//		floss {
//			// FLOSS build (no proprietary libraries)
//			dimension "version"
//		}
		google {
			// FLOSS build + Google libraries
			// Adds ChromeCast support
			dimension "version"
		}
	}

	packagingOptions {
		exclude 'META-INF/beans.xml'
	}

	lintOptions {
		checkReleaseBuilds false
	}
	buildToolsVersion '31.0.0'

	signingConfigs {
		debug {
			storeFile file('../debug.keystore')
		}
	}

	compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
	namespace 'github.daneren2005.dsub'
}

dependencies {
	implementation 'androidx.annotation:annotation:1.3.0'

	implementation project(path: ':ServerProxy')
	implementation fileTree(include: ['*.jar'], dir: 'libs')
	implementation 'androidx.core:core:1.8.0'
	implementation 'androidx.recyclerview:recyclerview:1.2.1'
	implementation 'androidx.appcompat:appcompat:1.1.0'
	implementation 'androidx.mediarouter:mediarouter:1.2.4' // Later versions: https://stackoverflow.com/questions/78965946/componentname-does-not-belong-to-sessionpackagename
	implementation 'com.google.android.material:material:1.0.0'
	implementation 'androidx.cardview:cardview:1.0.0'
	implementation 'com.google.android.gms:play-services-cast:21.3.0'
	implementation 'com.github.hannesa2:AndroidSlidingUpPanel:4.5.0'
	implementation 'de.hdodenhof:circleimageview:1.2.1'
    implementation 'com.shehabic.droppy:Droppy:0.5.1@aar'
	implementation 'androidx.lifecycle:lifecycle-process:2.3.1'
	implementation 'androidx.lifecycle:lifecycle-common-java8:2.3.1'
	implementation group: 'org.fourthline.cling', name: 'cling-core', version:'2.1.1'
	implementation group: 'org.fourthline.cling', name: 'cling-support', version:'2.1.1'
	implementation group: 'org.eclipse.jetty', name: 'jetty-server', version:'8.1.16.v20140903'
	implementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version:'8.1.16.v20140903'
	implementation group: 'org.eclipse.jetty', name: 'jetty-client', version:'8.1.16.v20140903'
	implementation group: 'javax.servlet', name: 'javax.servlet-api', version:'3.0.1'
}